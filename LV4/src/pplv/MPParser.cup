//import sekcija

package pplv;

import java_cup.runtime.*;
import java.io.*;
import java.util.*;
import pplv.SymbolTable.*;

parser code {:
   public int warnNo = 0;
   public int errorNo = 0;
   SymbolTable symbolTable;
   
   public static void main( String[] args )
   {
      try
      {
           FileReader file = new FileReader( args[0] );
           java_cup.runtime.Scanner scanner = new MPLexer( file );
           MPParser parser = new MPParser( scanner );
           parser.parse();
           parser.checkWarnings();
		   if ( parser.warnNo == 0 && parser.errorNo == 0 )
		      System.out.println( "Analiza zavrsena. U kodu nema gresaka." );
		   else
		   {
		      if ( parser.errorNo > 0 )
		         System.out.println( "Broj gresaka: " + parser.errorNo );
		      if ( parser.warnNo > 0 )
		         System.out.println( "Broj upozorenja: " + parser.warnNo );
		   }
      }
      catch( Exception e )
      {
           e.printStackTrace();
      }
   }
   
   public void checkWarnings()
   {
      SymbolNode current = symbolTable.getVariables();
      while ( current != null )
      {
      	Variable var = ( Variable ) current;
      	if ( var.last_def == -1 && var.last_use == -1 )
      	{
      		System.out.println( "Upozorenje: Promenljiva " + var.name + " je deklarisana, ali se nigde ne koristi." );
      		warnNo++;
      	}
      	else if ( var.last_def > var.last_use )
      	{
      		System.out.println( "Upozorenje: Vrednost dodeljena promeljivoj " + var.name + " u liniji " + var.last_def + " se nigde ne koristi." );
      		warnNo++;
      	}
      	current = current.next;
     }
   }
   
   public void syntax_error(Symbol cur_token)
   {
       
   }
   
   public void report_error(String message, int line)
   {
       System.out.println( "Greska u liniji " + line + ": " + message );
       errorNo++;
   }
   
   public int getLine()
   {
       return (( MPLexer) getScanner()).getLine();
   }

    boolean isAssignable(Type lhs, Type rhs)
    {
        if ( lhs == null || rhs == null ) return false;
        if ( lhs.tkind == rhs.tkind ) return true;
        if ( rhs.tkind == Type.INTEGER && lhs.tkind == Type.REAL ) return true;
        if ( rhs.tkind == Type.CHARACTER && lhs.tkind == Type.INTEGER ) return true;
        if ( rhs.tkind == Type.CHARACTER && lhs.tkind == Type.REAL ) return true;
        return false;
    }
:};
   
init with {:
    symbolTable = new SymbolTable();
:}


//Terminali
terminal PERIOD, PROGRAM, INTEGER, CHAR, REAL, BOOLEAN, BEGIN, END, ID, LEFTPAR, RIGHTPAR, ASSIGN, SELECT;
terminal CASE, THEN, SEMICOLON, COMMA, COLON, LESS, LESSEQUAL, GREATER, GREATEREQUAL, EQUAL, NOTEQUAL, AND, OR;

//Neterminali
non terminal Program, Block, Variables, Declaration, StatementList, Statement;
non terminal SelectStatement, CaseList, Case, RelOp;

non terminal List<String> NameList;
non terminal pplv.SymbolTable.Type Type;
non terminal pplv.SymbolTable.Type Expression, AndExpression, RelExpression, Term;

terminal Integer INTCONST;
terminal Double REALCONST;
terminal Character CHARCONST;
terminal Boolean BOOLCONST;

// Program ::= PROGRAM Block PERIOD
// Block ::= BEGIN Variables StatementList END 
// Variables ::= Variables Declaration | ε 
// Declaration ::= NameList COLON Type SEMICOLON 
// NameList ::= NameList COMMA ID | ID 
// Type ::= INTEGER | CHAR | REAL | BOOLEAN 
// StatementList  ::= Statement | StatementList Statement  
// Statement ::= SelectStatement | ID ASSIGN Expression SEMICOLON | Block 
// SelectStatement ::= SELECT BEGIN CaseList END 
// CaseList ::= CaseList Case | Case 
// Case ::= CASE Expression THEN Statement 
// Expression ::= Expression OR AndExpression | AndExpression  
// AndExpression ::= AndExpression AND RelExpression | RelExpression 
// RelExpression ::= Term RelOp Term | Term 
// RelOp ::=  LESS | LESSEQUAL | GREATER | GREATEREQUAL | EQUAL | NOTEQUAL
// Term ::= ID | CONST | LEFTPAR Expression RIGHTPAR

Program ::= PROGRAM Block PERIOD
            {: System.out.println( "Redukcija 1:  Program ::= PROGRAM Block PERIOD" ); :};

Block ::= BEGIN Variables StatementList END 
            {: System.out.println( "Redukcija 2:  Block ::= BEGIN Variables StatementList END " ); :};

Variables ::= Variables Declaration
            {: System.out.println( "Redukcija 3:  Variables ::= Variables Declaration" ); :}
            | /* ε */
            {: System.out.println( "Redukcija 4:  Variables ::= ε " ); :};

Declaration ::= NameList:names COLON Type:t SEMICOLON
            {:
                System.out.println( "Redukcija 5:  Declaration ::= NameList COLON Type SEMICOLON " );
                for ( String name : names ) {
                    if ( !parser.symbolTable.addVar( name, t ) ) {
                        parser.report_error( "Promenljiva " + name + " je vec deklarisana", parser.getLine() );
                    }
                }
            :};

NameList ::= NameList:list COMMA ID:id
            {: 
                System.out.println( "Redukcija 6:  NameList ::= NameList COMMA ID" );
                RESULT = list;
                RESULT.add( id.toString() );
            :}
            | ID:id
            {:
                System.out.println( "Redukcija 7:  NameList ::= ID " );
                RESULT = new ArrayList<>();
                RESULT.add( id.toString() );
            :};

Type ::= INTEGER
            {: 
                System.out.println( "Redukcija 8:  Type ::= INTEGER" );  
                RESULT = parser.symbolTable.getType("integer");
                if (RESULT == null) {
                    System.err.println("ERROR: getType(\"integer\") returned null!");
                }
            :}
            | CHAR
            {: 
                System.out.println( "Redukcija 9:  Type ::= CHAR" );     
                RESULT = parser.symbolTable.getType("char");
                if (RESULT == null) {
                    System.err.println("ERROR: getType(\"char\") returned null!");
                }
            :}
            | REAL
            {: 
                System.out.println( "Redukcija 10: Type ::= REAL" );     
                RESULT = parser.symbolTable.getType("real");
                if (RESULT == null) {
                    System.err.println("ERROR: getType(\"real\") returned null!");
                }
            :}
            | BOOLEAN
            {: 
                System.out.println( "Redukcija 11: Type ::= BOOLEAN " ); 
                RESULT = parser.symbolTable.getType("boolean");
                if (RESULT == null) {
                    System.err.println("ERROR: getType(\"boolean\") returned null!");
                }
            :};

StatementList ::= Statement
            {: System.out.println( "Redukcija 12: StatementList ::= Statement" ); :}
            | StatementList Statement  
            {: System.out.println( "Redukcija 13: StatementList ::= StatementList Statement  " ); :};

Statement ::= SelectStatement
            {: System.out.println( "Redukcija 14: Statement ::= SelectStatement" ); :}
            | ID:id ASSIGN Expression:expr SEMICOLON
            {:
                System.out.println( "Redukcija 15: Statement ::= ID ASSIGN Expression SEMICOLON" );
                String name = id.toString();
                Variable var = parser.symbolTable.getVar( name );

                if ( var == null )
                {
                    parser.report_error( "Promenljiva " + name + " nije deklarisana", parser.getLine() );
                }
                else
                {
                    Type lhs = var.type;
                    Type rhs = expr;
                    if ( !parser.isAssignable( lhs, rhs ) )
                    {
                        parser.report_error( "Neodgovarajuci tip u dodeli", parser.getLine() );
                    }
                    else
                    {
                        var.last_def = parser.getLine();
                    }
                }
            :}
            | Block 
            {: System.out.println( "Redukcija 16: Statement ::= Block " ); :};

SelectStatement ::= SELECT BEGIN CaseList END 
            {: System.out.println( "Redukcija 17: SelectStatement ::= SELECT BEGIN CaseList END " ); :};

CaseList ::= CaseList Case
            {: System.out.println( "Redukcija 18: CaseList ::= CaseList Case" ); :}
            | Case 
            {: System.out.println( "Redukcija 19: CaseList ::= Case " ); :};

Case ::= CASE Expression:expr THEN Statement
            {:
                System.out.println( "Redukcija 20: Case ::= CASE Expression THEN Statement " );
                if ( expr.tkind != Type.BOOLEAN )
                {
                    parser.report_error( "CASE izraz mora biti boolean", parser.getLine() );
                }
            :};

Expression ::= Expression:left OR AndExpression:right
            {:
                System.out.println( "Redukcija 21: Expression ::= Expression OR AndExpression" );
                if ( left.tkind != Type.BOOLEAN || right.tkind != Type.BOOLEAN )
                    parser.report_error( "OR zahteva boolean operande", parser.getLine() );
                RESULT = parser.symbolTable.getType("boolean");
            :}
            | AndExpression:expr
            {:
                System.out.println( "Redukcija 22: Expression ::= AndExpression  " );
                RESULT = expr;
            :};

AndExpression ::= AndExpression:left AND RelExpression:right
            {: 
                System.out.println( "Redukcija 23: AndExpression ::= AndExpression AND RelExpression" );
                if ( left.tkind != Type.BOOLEAN || right.tkind != Type.BOOLEAN )
                    parser.report_error( "AND zahteva boolean operande", parser.getLine() );
                RESULT = parser.symbolTable.getType("boolean");
            :}
            | RelExpression:expr
            {: 
                System.out.println( "Redukcija 24: AndExpression ::= RelExpression " );
                RESULT = expr;
            :};

RelExpression ::= Term:left RelOp Term:right
            {:
                System.out.println( "Redukcija 25: RelExpression ::= Term RelOp Term" );
                if ( left.tkind > Type.REAL || right.tkind > Type.REAL )
                {
                    parser.report_error( "Relacioni operator nad nenumerickim tipom", parser.getLine() );
                }
                RESULT = parser.symbolTable.getType("boolean");
            :}
            | Term:t
            {:
                System.out.println( "Redukcija 26: RelExpression ::= Term " );
                RESULT = t;
            :};

RelOp ::= LESS
            {: System.out.println( "Redukcija 27: RelOp ::= LESS" ); :}
            | LESSEQUAL
            {: System.out.println( "Redukcija 28: RelOp ::= LESSEQUAL" ); :}
            | GREATER
            {: System.out.println( "Redukcija 29: RelOp ::= GREATER" ); :}
            | GREATEREQUAL
            {: System.out.println( "Redukcija 30: RelOp ::= GREATEREQUAL" ); :}
            | EQUAL
            {: System.out.println( "Redukcija 31: RelOp ::= EQUAL" ); :}
            | NOTEQUAL
            {: System.out.println( "Redukcija 32: RelOp ::= NOTEQUAL" ); :};

Term ::= INTCONST:c
            {:
                System.out.println( "Redukcija 33: Term ::= INTCONST" );
                RESULT = parser.symbolTable.getType("integer");
                if (RESULT == null) System.err.println("ERROR: Term INTCONST - getType returned null");
            :}
            | REALCONST:c
            {:
                System.out.println( "Redukcija 34: Term ::= REALCONST" );
                RESULT = parser.symbolTable.getType("real");
                if (RESULT == null) System.err.println("ERROR: Term REALCONST - getType returned null");
            :}
            | CHARCONST:c
            {:
                System.out.println( "Redukcija 35: Term ::= CHARCONST" );
                RESULT = parser.symbolTable.getType("char");
                if (RESULT == null) {
                    RESULT = parser.symbolTable.getType("character");
                    if (RESULT == null) System.err.println("ERROR: Term CHARCONST - getType returned null");
                }
            :}
            | BOOLCONST:c
            {:
                System.out.println( "Redukcija 36: Term ::= BOOLCONST" );
                RESULT = parser.symbolTable.getType("boolean");
                if (RESULT == null) {
                    RESULT = parser.symbolTable.getType("boolean");
                    if (RESULT == null) System.err.println("ERROR: Term BOOLCONST - getType returned null");
                }
            :}
            | ID:id
            {:
                System.out.println( "Redukcija 37: Term ::= ID" );
                String name = id.toString();
                Variable var = parser.symbolTable.getVar(name);

                if (var == null)
                {
                    parser.report_error( "Promenljiva " + name + " nije deklarisana", parser.getLine() );
                    // Try to get unknown type, if it doesn't exist, use integer as fallback
                    Type unknownType = parser.symbolTable.getType("unknown");
                    RESULT = (unknownType != null) ? unknownType : parser.symbolTable.getType("integer");
                }
                else
                {
                    if (var.last_def == -1)
                        parser.report_error( "Promenljiva " + name + " se koristi pre inicijalizacije", parser.getLine() );

                    var.last_use = parser.getLine();
                    RESULT = var.type;
                }
            :}
            | LEFTPAR Expression:expr RIGHTPAR
            {:
                System.out.println( "Redukcija 38: Term ::= LEFTPAR Expression RIGHTPAR" );
                RESULT = expr;
            :};

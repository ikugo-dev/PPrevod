//import sekcija

import java_cup.runtime.*;
import java.io.*;

parser code {:
   public int errNo = 0;
   public static void main( String[] args )
   {
      try
	  {
		   FileReader file = new FileReader( args[0] );
		   MPLexer scanner = new MPLexer( file );
		   MPParser parser = new MPParser( scanner );
		   parser.parse();
		   if ( parser.errNo == 0 )
		      System.out.println( "Analiza zavrsena. U kodu nema gresaka." );
		   else
		      System.out.println( "Analiza zavrsena. Broj gresaka: " + parser.errNo );
	  }
	  catch( Exception e )
	  {
		   e.printStackTrace();
	  }
   }
   
   public void syntax_error(Symbol cur_token)
   {
   	  // nema nista?
   }
   
   public void report_error(String message, Object info)
   {
   	   System.out.print( message );
   }
   
   public int getLine()
   {
   	  return (( MPLexer) getScanner()).getLine() - 1;
   }
:};

//Terminali
terminal PERIOD, PROGRAM, INTEGER, CHAR, REAL, BOOLEAN, BEGIN, END, ID, CONST, LEFTPAR, RIGHTPAR, ASSIGN, SELECT;
terminal CASE, THEN, SEMICOLON, COMMA, COLON, LESS, LESSEQUAL, GREATER, GREATEREQUAL, EQUAL, NOTEQUAL, AND, OR;

//Neterminali
non terminal Program, Block, Variables, Declaration, NameList, Type, StatementList, Statement;
non terminal SelectStatement, CaseList, Case, Expression, AndExpression, RelExpression, RelOp, Term;

// Program ::= PROGRAM Block PERIOD
// Block ::= BEGIN Variables StatementList END 
// Variables ::= Variables Declaration | ε 
// Declaration ::= NameList COLON Type SEMICOLON 
// NameList ::= NameList COMMA ID | ID 
// Type ::= INTEGER | CHAR | REAL | BOOLEAN 
// StatementList  ::= Statement | StatementList Statement  
// Statement ::= SelectStatement | ID ASSIGN Expression SEMICOLON | Block 
// SelectStatement ::= SELECT BEGIN CaseList END 
// CaseList ::= CaseList Case | Case 
// Case ::= CASE Expression THEN Statement 
// Expression ::= Expression OR AndExpression | AndExpression  
// AndExpression ::= AndExpression AND RelExpression | RelExpression 
// RelExpression ::= Term RelOp Term | Term 
// RelOp ::=  LESS | LESSEQUAL | GREATER | GREATEREQUAL | EQUAL | NOTEQUAL
// Term ::= ID | CONST | LEFTPAR Expression RIGHTPAR

//------------------------------------------------------------------------------
Program ::= PROGRAM Block PERIOD
            {: System.out.println( "Redukcija 1:  Program ::= PROGRAM Block PERIOD" ); :}
            | PROGRAM Block error
            {: System.out.println( "Nedostaje '.' na kraju programa" ); parser.errNo++; :} 
            | PROGRAM error
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Telo programa je nekorektno."); parser.errNo++; :}
            | error
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nedostaje kljucna rec 'program' na pocetku."); parser.errNo++; :};
//------------------------------------------------------------------------------
Block ::= BEGIN Variables StatementList END 
            {: System.out.println( "Redukcija 2:  Block ::= BEGIN Variables StatementList END " ); :}
            | BEGIN Variables StatementList error 
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nezavrsen blok (nedostaje kljucna rec 'end')."); parser.errNo++; :};

//------------------------------------------------------------------------------
Variables ::= Variables Declaration
            {: System.out.println( "Redukcija 3:  Variables ::= Variables Declaration" ); :}
            | Variables error
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nedostaje deklaracija za promenjive"); parser.errNo++; :}
            | // ε 
            {: System.out.println( "Redukcija 4:  Variables ::= ε " ); :};

//------------------------------------------------------------------------------
Declaration ::= NameList COLON Type SEMICOLON 
            {: System.out.println( "Redukcija 5:  Declaration ::= NameList COLON Type SEMICOLON " ); :}
            | NameList COLON Type error  
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nedostaje ';'."); parser.errNo++; :}
            | NameList COLON error  
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nedostaje tip promenjive."); parser.errNo++; :}
            | NameList error
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nedostaje ',' ili ':'."); parser.errNo++; :};

//------------------------------------------------------------------------------
NameList ::= NameList COMMA ID
            {: System.out.println( "Redukcija 6:  NameList ::= NameList COMMA ID" ); :}
            | NameList COMMA error
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nevalidan ID promenjive"); parser.errNo++; :}
            | ID 
            {: System.out.println( "Redukcija 7:  NameList ::= ID " ); :};

//------------------------------------------------------------------------------
Type ::= INTEGER
            {: System.out.println( "Redukcija 8:  Type ::= INTEGER" ); :}
            | CHAR
            {: System.out.println( "Redukcija 9:  Type ::= CHAR" ); :}
            | REAL
            {: System.out.println( "Redukcija 10: Type ::= REAL" ); :}
            | BOOLEAN 
            {: System.out.println( "Redukcija 11: Type ::= BOOLEAN " ); :};

//------------------------------------------------------------------------------
StatementList ::= Statement
            {: System.out.println( "Redukcija 12: StatementList ::= Statement" ); :}
            | StatementList Statement  
            {: System.out.println( "Redukcija 13: StatementList ::= StatementList Statement  " ); :};

//------------------------------------------------------------------------------
Statement ::= SelectStatement
            {: System.out.println( "Redukcija 14: Statement ::= SelectStatement" ); :}
            | ID ASSIGN Expression SEMICOLON
            {: System.out.println( "Redukcija 15: Statement ::= ID ASSIGN Expression SEMICOLON" ); :}
            | ID ASSIGN Expression error
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nedostaje ';'"); parser.errNo++; :}
            | ID ASSIGN error
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Potreban je izraz sa desne strane ':=' simbola."); parser.errNo++; :}
            | Block 
            {: System.out.println( "Redukcija 16: Statement ::= Block " ); :};

//------------------------------------------------------------------------------
SelectStatement ::= SELECT BEGIN CaseList END 
            {: System.out.println( "Redukcija 17: SelectStatement ::= SELECT BEGIN CaseList END " ); :}
            | SELECT BEGIN CaseList error 
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nezavrsen 'select' izraz (nedostaje kljucna rec 'end')."); parser.errNo++; :}
            | SELECT BEGIN error 
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nevalidan slucaj u 'select' izrazu."); parser.errNo++; :}
            | SELECT error 
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Lose zapocet 'select' izraz (nedostaje kljucna rec 'begin')."); parser.errNo++; :};

//------------------------------------------------------------------------------
CaseList ::= CaseList Case
            {: System.out.println( "Redukcija 18: CaseList ::= CaseList Case" ); :}
            | Case 
            {: System.out.println( "Redukcija 19: CaseList ::= Case " ); :};

//------------------------------------------------------------------------------
Case ::= CASE Expression THEN Statement 
            {: System.out.println( "Redukcija 20: Case ::= CASE Expression THEN Statement " ); :}
            | CASE Expression THEN error 
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nevalidan izraz sa desne strane slucaja."); parser.errNo++; :}
            | CASE Expression error 
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nedostaje '=>'"); parser.errNo++; :}
            | CASE error 
            {: System.out.println(  "-> Greska u liniji " + parser.getLine() + ": " + "Nevalidan izraz u slucaju."); parser.errNo++; :};

//------------------------------------------------------------------------------
Expression ::= Expression OR AndExpression
            {: System.out.println( "Redukcija 21: Expression ::= Expression OR AndExpression" ); :}
            | AndExpression  
            {: System.out.println( "Redukcija 22: Expression ::= AndExpression  " ); :};

//------------------------------------------------------------------------------
AndExpression ::= AndExpression AND RelExpression
            {: System.out.println( "Redukcija 23: AndExpression ::= AndExpression AND RelExpression" ); :}
            | RelExpression 
            {: System.out.println( "Redukcija 24: AndExpression ::= RelExpression " ); :};

//------------------------------------------------------------------------------
RelExpression ::= Term RelOp Term
            {: System.out.println( "Redukcija 25: RelExpression ::= Term RelOp Term" ); :}
            | Term 
            {: System.out.println( "Redukcija 26: RelExpression ::= Term " ); :};

//------------------------------------------------------------------------------
RelOp ::= LESS
            {: System.out.println( "Redukcija 27: RelOp ::= LESS" ); :}
            | LESSEQUAL
            {: System.out.println( "Redukcija 28: RelOp ::= LESSEQUAL" ); :}
            | GREATER
            {: System.out.println( "Redukcija 29: RelOp ::= GREATER" ); :}
            | GREATEREQUAL
            {: System.out.println( "Redukcija 30: RelOp ::= GREATEREQUAL" ); :}
            | EQUAL
            {: System.out.println( "Redukcija 31: RelOp ::= EQUAL" ); :}
            | NOTEQUAL
            {: System.out.println( "Redukcija 32: RelOp ::= NOTEQUAL" ); :};

//------------------------------------------------------------------------------
Term ::= ID
            {: System.out.println( "Redukcija 33: Term ::= ID" ); :}
            | CONST
            {: System.out.println( "Redukcija 34: Term ::= CONST" ); :}
            | LEFTPAR Expression RIGHTPAR
            {: System.out.println( "Redukcija 35: Term ::= LEFTPAR Expression RIGHTPAR" ); :};
